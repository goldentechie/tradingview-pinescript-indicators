//@version=3
// Copyright (c) 2018-present, Alex Orekhov (everget)
// Chande Momentum Oscillator script may be freely distributed under the MIT license.
study("Chande Momentum Oscillator", shorttitle="CMO")

length = input(title="Length", type=integer, minval=1, defval=9)
overboughtLevel = input(title="Overbought", type=integer, defval=50)
oversoldLevel = input(title="Oversold", type=integer, defval=-50)
src = input(title="Source", type=source, defval=close)

up(mom) => mom >= 0 ? mom : 0
down(mom) => mom >= 0 ? 0 : -mom

getCMO(src, length) =>
	mom = change(src)

	upSum = sum(up(mom), length)
	downSum = sum(down(mom), length)

	cmo = 100 * (upSum - downSum) / (upSum + downSum)

cmo = getCMO(src, length)

cmoColor = cmo > overboughtLevel ? #29e038 : cmo < oversoldLevel ? red : #0094ff

plot(cmo, title="CMO", linewidth=2, color=cmoColor, transp=0)

hline(0, title="Zero Level", linestyle=line, color=gray)

overboughtBand = hline(overboughtLevel, linestyle=dashed, color=gray)
oversoldBand = hline(oversoldLevel, linestyle=dashed, color=gray)
fill(overboughtBand, oversoldBand, color=#f9cb9c0f)
