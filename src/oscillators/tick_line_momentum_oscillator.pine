//@version=3
// Copyright (c) 2018-present, Alex Orekhov (everget)
// Tick Line Momentum Oscillator script may be freely distributed under the MIT license.
study("Tick Line Momentum Oscillator", shorttitle="TLMO")

cumoMALength = input(title="Cumo MA Length", type=integer, minval=1, defval=10)
cumoMAInput = input(title="Cumo MA", defval="EMA", options=["EMA", "SMA", "VWMA", "WMA"])
rocLength = input(title="ROC Length", type=integer, minval=1, defval=5)
smoothingLength = input(title="Smoothing Length", type=integer, minval=1, defval=5)
smoothingMAInput = input(title="Smoothing MA", defval="EMA", options=["EMA", "SMA", "VWMA", "WMA"])
obosLevel = input(title="Overbought / Oversold Level", type=integer, minval=1, defval=5)
src = input(title="Source", type=source, defval=close)

cumoMA = iff(cumoMAInput == "EMA", ema(src, cumoMALength),
	 iff(cumoMAInput == "SMA", sma(src, cumoMALength),
	 iff(cumoMAInput == "VWMA", vwma(src, cumoMALength),
	 iff(cumoMAInput == "WMA", wma(src, cumoMALength),
	 na))))

cumoExpression = src > nz(cumoMA[1])
	 ? 1
	 : src < nz(cumoMA[1])
	 	 ? -1
	 	 : 0

tlmo = iff(smoothingMAInput == "EMA", ema(roc(cum(cumoExpression), rocLength), smoothingLength),
	 iff(smoothingMAInput == "SMA", sma(roc(cum(cumoExpression), rocLength), smoothingLength),
	 iff(smoothingMAInput == "VWMA", vwma(roc(cum(cumoExpression), rocLength), smoothingLength),
	 iff(smoothingMAInput == "WMA", wma(roc(cum(cumoExpression), rocLength), smoothingLength),
	 na))))

tlmoColor = tlmo > obosLevel ? #0ebb23 : tlmo < -obosLevel ? #ff0000 : #674ea7
plot(tlmo, title="TLMO", linewidth=2, color=tlmoColor, transp=0)

hline(obosLevel, title="Overbought", linestyle=dotted, color=#0ebb23)
hline(0, title="Zero Level", linestyle=solid, color=#f6b26b)
hline(-1 * obosLevel, title="Oversold", linestyle=dotted, color=#ff0000)
