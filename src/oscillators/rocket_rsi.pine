//@version=3
// Copyright (c) 2018-present, Alex Orekhov (everget)
// Rocket RSI script may be freely distributed under the MIT license.
study("Rocket RSI", shorttitle="Rocket RSI")

rsiLength = input(title="RSI Length", type=integer, minval=1, defval=10)
smoothingLength = input(title="Smoothing Length", type=integer, minval=1, defval=8)
obosLevel = input(title="Overbought / Oversold Level", type=float, minval=0.0, defval=2.0)
applyNormalization = input(title="Apply Normalization to [-100, 100] values ?", type=bool, defval=false)
src = input(title="Source", type=source, defval=close)

PI = 2 * asin(1)

a1 = 0.0
b1 = 0.0
c1 = 0.0
c2 = 0.0
c3 = 0.0

// Compute Super Smoother coefficients
a1 := exp(-1.414 * PI / smoothingLength)
b1 := 2 * a1 * cos(1.414 * 180 / smoothingLength)
c2 := b1
c3 := -a1 * a1
c1 := 1 - c2 - c3

// Create half dominant cycle momentum
mom = change(src, rsiLength - 1)

// Super Smoother Filter
ssf = 0.0
ssf := c1 * (mom + nz(mom[1])) / 2 + c2 * nz(ssf[1]) + c3 * nz(ssf[2])

// Accumulate `Closes Up` and `Closes Down`
upSum = sum(change(ssf, 1) > 0 ? change(ssf, 1) : 0.0, rsiLength)
downSum = sum(change(ssf, 1) > 0 ? 0.0 : abs(change(ssf, 1)), rsiLength)

tmpRSI = 0.0
tmpRSI := upSum + downSum != 0 ? (upSum - downSum) / (upSum + downSum) : nz(tmpRSI[1])

// Limit RocketRSI output to +/-3 Standard Deviations
if tmpRSI > 0.999
	tmpRSI := 0.999

if tmpRSI < -0.999
	tmpRSI := -0.999

// Apply Fisher Transform to establish Gaussian Probability Distribution
rocketRSI = 0.5 * log((1 + tmpRSI) / (1 - tmpRSI))
rocketRSI := applyNormalization ? rocketRSI * 25 : rocketRSI

overboughtLevel = applyNormalization ? obosLevel * 25 : obosLevel
oversoldLevel = applyNormalization ? obosLevel * -25 : obosLevel * -1

rocketRSIColor = rocketRSI > overboughtLevel ? green : rocketRSI < oversoldLevel ? red : #ff9370

plot(rocketRSI, title="RRSI", linewidth=2, color=rocketRSIColor, transp=0)

plot(overboughtLevel, title="OB Level", style=circles, color=#3d85c6, transp=0)
hline(0, title="Zero Level", linestyle=dotted)
plot(oversoldLevel, title="OS Level", style=circles, color=#3d85c6, transp=0)
