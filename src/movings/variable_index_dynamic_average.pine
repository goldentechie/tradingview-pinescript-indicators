//@version=3
// Copyright (c) 2018-present, Alex Orekhov (everget)
// Variable Index Dynamic Average indicator script may be freely distributed under the MIT license.
study(title="Variable Index Dynamic Average", shorttitle="VIDYA", overlay=true)

length = input(title="Length", type=integer, defval=9, minval=1)
src = input(title="Source", type=source, defval=close)

// Chande Momentum Oscillator
f1(m) => m >= 0.0 ? m : 0.0
f2(m) => m >= 0.0 ? 0.0 : -m

diff = change(src)
upSum = sum(f1(diff), length)
downSum = sum(f2(diff), length)

cmo = (upSum - downSum) / (upSum + downSum)

factor = 2 / (length + 1)

vidya = 0.0
vidya := src * factor * abs(cmo) + nz(vidya[1]) * (1 - factor * abs(cmo))

plot(vidya, title="VIDYA", color=#741b47, linewidth=2, transp=0)
