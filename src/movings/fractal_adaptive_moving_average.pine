//@version=3
// Copyright (c) 2018-present, Alex Orekhov (everget)
// Fractal Adaptive Moving Average script may be freely distributed under the MIT license.
study(title="Fractal Adaptive Moving Average", shorttitle="FRAMA", overlay=true)

length = input(title="Length", type=integer, minval=1, defval=20)
src = input(title="Source", type=source, defval=close)

frama(src, length) =>
	length2 = round(length / 2)

	hh2 = highest(high, length2)
	ll2 = lowest(low, length2)

	N1 = (hh2 - ll2) / length2
	N2 = (hh2[length2] - ll2[length2]) / length2
	N3 = (highest(high, length) - lowest(low, length)) / length
	D = (log(N1 + N2) - log(N3)) / log(2)

	alpha = exp(-4.6 * (D - 1))

	frama = 0.0
	frama := alpha * src + (1 - alpha) * nz(frama[1])

plot(frama(src, length), title="FRAMA", color=#741b47, linewidth=2, transp=0)
