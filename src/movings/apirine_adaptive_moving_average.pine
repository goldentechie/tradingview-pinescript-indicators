//@version=3
// Copyright (c) 2018-present, Alex Orekhov (everget)
// Adaptive Moving Average script may be freely distributed under the MIT license.
study("Adaptive Moving Average", shorttitle="AMA", overlay=true)

length = input(title="Length", type=integer, minval=1, defval=14)
fastLength = input(title="Fast MA Length", type=integer, minval=1, defval=2)
slowLength = input(title="Slow MA Length", type=integer, minval=1, defval=30)
src = input(title="Source", type=source, defval=close)

ama(src, length, fastLength, slowLength) =>
	pds = length + 1
	fastSC = 2 / (fastLength + 1)
	slowSC = 2 / (slowLength + 1)

	MLTP = abs((src - lowest(low, pds)) - (highest(high, pds) - src)) / (highest(high, pds) - lowest(low, pds))

	SSC = MLTP * (fastSC - slowSC) + slowSC

	ama = 0.0
	ama := nz(ama[1]) + (SSC * SSC) * (src - nz(ama[1]))

plot(ama(src, length, fastLength, slowLength), title="AMA", linewidth=2, color=#741b47, transp=0)
