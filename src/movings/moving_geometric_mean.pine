//@version=3
// Copyright (c) 2018-present, Alex Orekhov (everget)
// Geometric Mean indicator script may be freely distributed under the MIT license.
study("Geometric Mean", shorttitle="GM", overlay=true)

length = input(title="Length", type=integer, defval=14, minval=1)
src = input(title="Source", type=source, defval=close)

// https://en.wikipedia.org/wiki/Nth_root_algorithm
// https://en.wikipedia.org/wiki/Nth_root
// http://www.calculator.net/root-calculator.html?cvar3=14&cvar4=5&ctype=3&x=77&y=23
// https://www.rapidtables.com/calc/math/Root_Calculator.html
// https://rosettacode.org/wiki/Nth_root#JavaScript

precision = 0.00001

nthRoot(value, degree) =>
	x0 = 1
	xk = ((degree - 1) * x0 + value / pow(x0, degree - 1)) / degree
	xkDelta = (value / (pow(x0, degree - 1)) - x0) / degree

	for i = 0 to 99999
		if abs(xkDelta) <= precision
			break

		xkDelta := (value / (pow(xk, degree - 1)) - xk) / degree
		xk := xk + xkDelta
	xk

gm(src, length) =>
	prod = 1.0

	for i = 0 to length - 1
		prod := prod * (na(src[i]) ? 1 : src[i])

	nthRoot(prod, length)

plot(gm(src, length), title="GM", linewidth=2, color=#6d1e7f, transp=0)
