//@version=3
// Copyright (c) 2018-present, Alex Orekhov (everget)
// Variable Index Dynamic Average script may be freely distributed under the MIT license.
study("Variable Index Dynamic Average", shorttitle="VIDYA", overlay=true)

length = input(title="Length", type=integer, minval=1, defval=9)
src = input(title="Source", type=source, defval=close)

vidya(src, length) =>
	// Chande Momentum Oscillator
	diff = change(src)
	upSum = sum(diff >= 0.0 ? diff : 0.0, length)
	downSum = sum(diff >= 0.0 ? 0.0 : -diff, length)

	cmo = (upSum - downSum) / (upSum + downSum)

	factor = 2 / (length + 1)

	vidya = 0.0
	vidya := src * factor * abs(cmo) + (1 - factor * abs(cmo)) * nz(vidya[1])

plot(vidya(src, length), title="VIDYA", color=#741b47, linewidth=2, transp=0)
