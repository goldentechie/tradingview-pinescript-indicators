//@version=3
// Copyright (c) 2018-present, Alex Orekhov (everget)
// DeMarker script may be freely distributed under the MIT license.
study("DeMarker", shorttitle="DeMarker")

length = input(title="Length", type=integer, minval=1, defval=14)
maType = input(title="MA Type", defval="SMA", options=["EMA", "SMA", "VWMA", "WMA"])
overboughtLevel = input(title="Overbought Level", type=float, step=0.1, defval=0.7)
oversoldLevel = input(title="Oversold Level", type=float, step=0.1, defval=0.3)
src = input(title="Source", type=source, defval=close)

getMA(type, src, length) =>
	type == "SMA" ? sma(src, length) :
	 type == "EMA" ? ema(src, length) :
	 type == "VWMA" ? vwma(src, length) :
	 type == "WMA" ? wma(src, length) :
	 na

demax = change(high) > 0 ? change(high) : 0
demin = change(low) < 0 ? -change(low) : 0

demarker = getMA(maType, demax, length) / (getMA(maType, demax, length) + getMA(maType, demin, length))

demarkerColor = demarker > overboughtLevel ? #0ebb23 : demarker < oversoldLevel ? #cc0000 : #674ea7

plot(demarker, title="DeMarker", linewidth=2, color=demarkerColor, transp=0)

hline(overboughtLevel, title="Overbought", linestyle=dotted, color=#e69138)
hline(oversoldLevel, title="Oversold", linestyle=dotted, color=#e69138)
