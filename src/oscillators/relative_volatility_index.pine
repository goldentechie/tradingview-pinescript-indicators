//@version=3
// Copyright (c) 2018-present, Alex Orekhov (everget)
// Relative Volatility Index script may be freely distributed under the MIT license.
study("Relative Volatility Index", shorttitle="RVI")

stdevLength = input(title="Standard Deviation Length", type=integer, minval=0, defval=10)
stdevInput = input(title="Standard Deviation", defval="Sample", options=["Population", "Sample"])
emaLength = input(title="EMA Length", type=integer, minval=1, defval=14)
highBand = input(title="High Band", type=integer, maxval=100, defval=80)
lowBand = input(title="Low Band", type=integer, minval=0, defval=20)
useWilderSmoothing = input(title="Use Wilder's Smoothing ?", type=bool, defval=true)
useRefined = input(title="Use refined RVI ?", type=bool, defval=true)
src = input(title="Source", type=source, defval=close)

sampleStdev(src, length) =>
	dev = src - sma(src, length)
	variance = sum(dev * dev, length) / (length - 1)
	sqrt(variance)

selectedStdev = stdevInput == "Sample"
	 ? sampleStdev(src, stdevLength)
	 : stdev(src, stdevLength)

// Relative Volatility Index
up(src) => change(src) > 0 ? selectedStdev : 0
down(src) => change(src) > 0 ? 0 : selectedStdev

// Original version (1993)
rviOriginal(src) =>
	upSum = ema(up(src), emaLength)
	downSum = ema(down(src), emaLength)
	100 * upSum / (upSum + downSum)

// Refined version (1995)
rvi = useRefined ? (rviOriginal(high) + rviOriginal(low)) / 2 : rviOriginal(src)

plot(rvi, title="RVI", color=#008000, transp=0)

highBandPlot = hline(price=highBand, linestyle=dashed)
hline(price=50, linestyle=dotted)
lowBandPlot = hline(price=lowBand, linestyle=dashed)

fill(hline1=highBandPlot, hline2=lowBandPlot, color=#996a15, transp=90)
