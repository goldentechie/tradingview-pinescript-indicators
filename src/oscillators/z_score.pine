//@version=3
// Copyright (c) 2018-present, Alex Orekhov (everget)
// Z-Score script may be freely distributed under the MIT license.
study("Z-Score", shorttitle="ZSC")

length = input(title="Length", type=integer, defval=14, minval=1)
stdevInput = input(title="Standard Deviation Algorithm", defval="Built-In (Naive)", options=["Built-In (Naive)", "Two-Pass"])
src = input(title="Source", type=source, defval=close)

// https://en.wikipedia.org/wiki/Algorithms_for_calculating_variance#Two-pass_algorithm
twoPassStdev(src, length) =>
	dev = src - sma(src, length)
	variance = sum(dev * dev, length) / (length - 1)
	sqrt(variance)

selectedStdev = stdevInput == "Built-In (Naive)" ? stdev(src, length) :
	 stdevInput == "Two-Pass" ? twoPassStdev(src, length) :
	 na

zsc = (src - sma(src, length)) / selectedStdev

zscColor = zsc > 2.0 ? #0ebb23 : zsc < -2.0 ? #ff0000 : #8e7cc3

plot(zsc, title="Z-Score", linewidth=2, color=zscColor, transp=0, style=line)

hline(title="Upper Band", price=2.0, linestyle=dotted, color=#e69138)
hline(0, title="Zero Level", linestyle=dotted, color=#989898)
hline(title="Lower Band", price=-2.0, linestyle=dotted, color=#e69138)
