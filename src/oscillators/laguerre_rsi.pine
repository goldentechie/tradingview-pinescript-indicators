//@version=3
// Copyright (c) 2018-present, Alex Orekhov (everget)
// Laguerre RSI script may be freely distributed under the MIT license.
study("Laguerre RSI", shorttitle="LRSI")

alpha = input(title="Alpha", type=float, minval=0, step=0.1, defval=0.7)
src = input(title="Source", type=source, defval=close)

L0 = 0.0
L1 = 0.0
L2 = 0.0
L3 = 0.0

L0 := alpha * src + (1 - alpha) * nz(L0[1])
L1 := -(1 - alpha) * L0 + nz(L0[1]) + (1 - alpha) * nz(L1[1])
L2 := -(1 - alpha) * L1 + nz(L1[1]) + (1 - alpha) * nz(L2[1])
L3 := -(1 - alpha) * L2 + nz(L2[1]) + (1 - alpha) * nz(L3[1])

CU = 0.0
CD = 0.0

CU := (L0 >= L1 ? L0 - L1 : 0) + (L1 >= L2 ? L1 - L2 : 0) + (L2 >= L3 ? L2 - L3 : 0)
CD := (L0 >= L1 ? 0 : L1 - L0) + (L1 >= L2 ? 0 : L2 - L1) + (L2 >= L3 ? 0 : L3 - L2)

lrsi = iff(CU + CD != 0, CU / (CU + CD), 0)

overboughtLevel = hline(0.8, title="Overbought Level", linestyle=dashed, color=#686868)
oversoldLevel = hline(0.2, title="Oversold Level", linestyle=dashed, color=#686868)

plot(lrsi, title="LRSI", linewidth=2, color=#ff3e7d, transp=0)
